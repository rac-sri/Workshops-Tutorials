buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE")
        classpath("gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:5.0.7")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'application'
apply plugin: 'org.flywaydb.flyway'

mainClassName = "com.ej.Application"

jar {
    baseName = 'jenkins_3_database_migration'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework:spring-jdbc")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("com.h2database:h2")
    compile("mysql:mysql-connector-java")
    compile("org.flywaydb:flyway-core:5.0.7")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.assertj:assertj-core:3.0.0")
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}

springBoot {
  executable = true
  buildInfo()
}

jacocoTestReport {
  reports {
    html.enabled = true
    xml.enabled = true
    csv.enabled = true
  }
}
test.finalizedBy jacocoTestReport

jacoco {
    applyTo run
}

task applicationCodeCoverageReport(type:JacocoReport){
    executionData run
    sourceSets sourceSets.main
}

flyway {
    //integration test database
    url = 'jdbc:h2:./database/user_mgt;AUTO_SERVER=TRUE'
    user = 'sa'
    password = 'sa'
}

task migrateStaging(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
    url = 'jdbc:mysql://192.168.50.6:3306/user_mgt'
    user = 'springuser'
    password = 'xXuVBAnXsRm3R2eT'
}
